# -*- mode: python -*-
# OpenPGM build script

import platform
import os
import time
import sys

EnsureSConsVersion( 0, 97 )
SConsignFile('scons.signatures');
SConsignFile('scons.signatures'+ '-' + platform.system() + '-' + platform.machine());

opt = Options(None, ARGUMENTS)
opt.AddOptions (
	(EnumOption ('BUILD', 'build environment', 'debug', ('release', 'debug', 'profile'))),
	(EnumOption ('BRANCH', 'branch prediction', 'none', ('none', 'profile', 'seed'))),
	(EnumOption ('WITH_GETTEXT', 'l10n support via libintl', 'false', ('true', 'false'))),
	(EnumOption ('WITH_GLIB', 'Build GLib dependent modules', 'false', ('true', 'false'))),
	(EnumOption ('COVERAGE', 'test coverage', 'none', ('none', 'full'))),
	(EnumOption ('WITH_HISTOGRAMS', 'Runtime statistical information', 'true', ('true', 'false'))),
	(EnumOption ('WITH_HTTP', 'HTTP administration', 'true', ('true', 'false'))),
	(EnumOption ('WITH_SNMP', 'SNMP administration', 'true', ('true', 'false'))),
	(EnumOption ('WITH_CHECK', 'Check test system', 'false', ('true', 'false'))),
	(EnumOption ('WITH_TEST', 'Network test system', 'false', ('true', 'false'))),
	(EnumOption ('WITH_CC', 'C++ Examples', 'true', ('true', 'false'))),
	(EnumOption ('WITH_EXAMPLES', 'Examples', 'true', ('true', 'false'))),
	(EnumOption ('WITH_NCURSES', 'NCURSES examples', 'false', ('true', 'false'))),
	(EnumOption ('WITH_PROTOBUF', 'Google Protocol Buffer examples', 'false', ('true', 'false')))
)

if platform.machine() == 'x86_64':
	pkg_config_path='/usr/evolution28/lib64/pkgconfig:/usr/lib/pkconfig';
else:
	pkg_config_path='/usr/evolution28/lib/pkgconfig:/usr/lib/pkconfig';

#-----------------------------------------------------------------------------
# Dependencies

env = Environment();

def CheckPKGConfig(context, version):
	context.Message( 'Checking for pkg-config... ' )
	ret = context.TryAction('PKG_CONFIG_PATH=%s pkg-config --atleast-pkgconfig-version=%s' % (pkg_config_path, version))[0]
	context.Result( ret )
	return ret

def CheckPKG(context, name):
	context.Message( 'Checking for %s... ' % name )
	ret = context.TryAction('PKG_CONFIG_PATH=%s pkg-config --exists \'%s\'' % (pkg_config_path, name))[0]
	context.Result( ret )
	return ret

conf = Configure(env, custom_tests = { 'CheckPKGConfig' : CheckPKGConfig,
				       'CheckPKG' : CheckPKG })

if not conf.CheckPKGConfig('0.15.0'):
	print 'pkg-config >= 0.15.0 not found.'
#	Exit(1)

if not conf.CheckPKG('glib-2.0 >= 2.10'):
	print 'glib-2.0 >= 2.10 not found.'
#	Exit(1)

if not conf.CheckPKG('gthread-2.0'):
	print 'gthread-2.0 not found.'
#	Exit(1)

env = conf.Finish();

#-----------------------------------------------------------------------------
# Platform specifics

env = Environment(ENV = os.environ,
	CCFLAGS = [	'-pipe',
			'-Wall',
				'-Wextra',
				'-Wfloat-equal',
				'-Wshadow',
				'-Wpointer-arith',
				'-Wbad-function-cast',
				'-Wcast-qual',
				'-Wcast-align',
				'-Wwrite-strings',
				'-Waggregate-return',
				'-Wstrict-prototypes',
				'-Wold-style-definition',
				'-Wmissing-prototypes',
				'-Wmissing-declarations',
				'-Wmissing-noreturn',
				'-Wmissing-format-attribute',
				'-Wredundant-decls',
				'-Wnested-externs',
				'-Winline',
			'-pedantic',
# Native arch
			'-march=native',
# C99
			'-std=gnu99',
			'-D_XOPEN_SOURCE=600',
			'-D_BSD_SOURCE',
# re-entrant libc
			'-D_REENTRANT',
# POSIX spinlocks
			'-DHAVE_PTHREAD_SPINLOCK',
# NSS protocol lookup
			'-DHAVE_GETPROTOBYNAME_R',
#			'-DGETPROTOBYNAME_R_STRUCT_PROTOENT_P',
# NSS networks lookup, IPv4 only
			'-DHAVE_GETNETENT',
# variadic macros
 			'-DHAVE_ISO_VARARGS',
#			'-DHAVE_GNUC_VARARGS',
# stack memory api header
			'-DHAVE_ALLOCA_H',
# useful /proc system
			'-DHAVE_PROC_CPUINFO',
# example: crash handling
			'-DHAVE_BACKTRACE',
# timing
			'-DHAVE_FTIME',
			'-DHAVE_GETTIMEOFDAY',
			'-DHAVE_CLOCK_GETTIME',
			'-DHAVE_PSELECT',
			'-DHAVE_DEV_RTC',
			'-DHAVE_RDTSC',
# event handling
			'-DHAVE_POLL',
			'-DHAVE_EPOLL_CTL',
# interface enumeration
			'-DHAVE_GETIFADDRS',
			'-DHAVE_STRUCT_IFADDRS_IFR_NETMASK',
# win32 cmsg
#			'-DHAVE_WSACMSGHDR',
# multicast
			'-DHAVE_STRUCT_GROUP_REQ',
			'-DHAVE_STRUCT_IP_MREQN',
			'-DHAVE_STRUCT_IP_MSFILTER',
# sprintf
			'-DHAVE_SPRINTF_GROUPING',
			'-DHAVE_VASPRINTF',
# symbol linking scope
			'-DHAVE_DSO_VISIBILITY',
# socket binding
			'-DUSE_BIND_INADDR_ANY',
# IP header order as per IP(4) on FreeBSD
#			'-DHAVE_HOST_ORDER_IP_LEN',
#			'-DHAVE_HOST_ORDER_IP_OFF',
# ticket based spinlocks
			'-DUSE_TICKET_SPINLOCK',
# dumb read-write spinlock
			'-DUSE_DUMB_RWSPINLOCK',
# optimum galois field multiplication
			'-DUSE_GALOIS_MUL_LUT',
# GNU getopt
			'-DHAVE_GETOPT',
# temporary for pgmping
#			'-DCONFIG_WITH_HEATMAP',
#			'-DCONFIG_WITH_BUSYWAIT',
#			'-DCONFIG_HAVE_SCHEDPARAM'
		 ],
	LINKFLAGS = [	'-pipe'
		    ],
	LIBS = [
			'pthread',
# histogram math
			'm',
# clock_gettime()
			'rt'
		],
	PROTOBUF_CCFLAGS = '-I/home/steve-o/protobuf-2.5.0/gcc64/include',
	PROTOBUF_LIBS = '/home/steve-o/protobuf-2.5.0/gcc64/lib/libprotobuf.a',
	PROTOBUF_PROTOC = '/home/steve-o/protobuf-2.5.0/gcc64/bin/protoc'
)
opt.Update (env)

# Branch prediction
if env['BRANCH'] == 'profile':
	env.Append(CCFLAGS = '-fprofile-arcs')
	env.Append(LINKFLAGS = '-fprofile-arcs')
elif env['BRANCH'] == 'seed':
	env.Append(CCFLAGS = '-fbranch-probabilities')

# Coverage analysis
if env['COVERAGE'] == 'full':
	env.Append(CCFLAGS = '-fprofile-arcs')
	env.Append(CCFLAGS = '-ftest-coverage')
	env.Append(LINKFLAGS = '-fprofile-arcs')
	env.Append(LINKFLAGS = '-lgcov')

# Define separate build environments
release = env.Clone(BUILD = 'release')
release.Append(CCFLAGS = '-O2')

debug = env.Clone(BUILD = 'debug')
debug.Append(CCFLAGS = ['-DPGM_DEBUG','-ggdb'], LINKFLAGS = '-gdb')

profile = env.Clone(BUILD = 'profile')
profile.Append(CCFLAGS = ['-O0','-pg'], LINKFLAGS = '-pg')

thirtytwo = release.Clone(BUILD = 'thirtytwo')
thirtytwo.Append(CCFLAGS = '-m32', LINKFLAGS = '-m32')

# choose and environment to build
if env['BUILD'] == 'release':
	Export({'env':release})
elif env['BUILD'] == 'profile':
	Export({'env':profile})
elif env['BUILD'] == 'thirtytwo':
	Export({'env':thirtytwo})
else:
	Export({'env':debug})

#-----------------------------------------------------------------------------
# Re-analyse dependencies

Import('env')

# vanilla environment
if env['WITH_GLIB'] == 'true':
	env['GLIB_FLAGS'] = env.ParseFlags('!PKG_CONFIG_PATH=%s pkg-config --cflags --libs glib-2.0 gthread-2.0' % pkg_config_path);
else:
	env['GLIB_FLAGS'] = '';

# l10n
if env['WITH_GETTEXT'] == 'true':
	env.Append(CCFLAGS = '-DHAVE_GETTEXT');

# instrumentation
if env['WITH_HTTP'] == 'true' and env['WITH_HISTOGRAMS'] == 'true':
	env.Append(CCFLAGS = '-DUSE_HISTOGRAMS');

# managed environment for libpgmsnmp, libpgmhttp
if env['WITH_SNMP'] == 'true':
	env['SNMP_FLAGS'] = env.ParseFlags('!net-snmp-config --cflags --agent-libs');

def CheckSNMP(context):
	context.Message('Checking Net-SNMP...');
	lastLIBS = context.env['LIBS'];
	lastCCFLAGS= context.env['CCFLAGS'];
	context.env.MergeFlags(env['SNMP_FLAGS']);
	result = context.TryLink("""
int main(int argc, char**argv)
{
	init_agent("PGM");
	return 0;
}
""", '.c');
	context.env.Replace(LIBS = lastLIBS, CCFLAGS=lastCCFLAGS);
	context.Result(not result);
	return result;

def CheckCheck(context):
	context.Message('Checking Check unit test framework...');
	result = context.TryAction('pkg-config --cflags --libs check')[0];
	context.Result(result);
	return result;

def CheckEventFD(context):
	context.Message('Checking eventfd...');
	result = context.TryLink("""
#include <sys/eventfd.h>
int main(int argc, char**argv)
{
	eventfd(0,0);
	return 0;
}
""", '.c')
	context.Result(result);
	return result;

tests = {
	'CheckCheck':	CheckCheck,
	'CheckEventFD':	CheckEventFD
}
if env['WITH_SNMP'] == 'true':
	tests['CheckSNMP'] = CheckSNMP;
conf = Configure(env, custom_tests = tests);

if env['WITH_SNMP'] == 'true' and not conf.CheckSNMP():
	print 'Enabling extra Red Hat dependencies for Net-SNMP.';
	conf.env['SNMP_FLAGS']['LIBS'].append(['librpm', 'libsensors', 'libdl', 'libwrap']);
	lastLIBS = conf.env['LIBS'];
	conf.env.ParseConfig('perl -MExtUtils::Embed -e ldopts');
	conf.env['SNMP_FLAGS']['LIBS'].append(conf.env['LIBS']);
	conf.env.Replace(LIBS = lastLIBS);
	if not conf.CheckSNMP():
		print 'Net-SNMP libraries not compatible.';
		Exit(1);

if env['WITH_CHECK'] == 'true' and conf.CheckCheck():
	print 'Enabling Check unit tests.';
	conf.env['CHECK'] = 'true';
	env['CHECK_FLAGS'] = env.ParseFlags('!pkg-config --cflags --libs check');
else:
	print 'Disabling Check unit tests.';
	conf.env['CHECK'] = 'false';

if conf.CheckEventFD():
	print 'Enabling kernel eventfd notification mechanism.';
	conf.env.Append(CCFLAGS = '-DHAVE_EVENTFD');

env = conf.Finish();

# add builder to create PIC static libraries for including in shared libraries
action_list = [ Action("$ARCOM", "$ARCOMSTR") ];
if env.Detect('ranlib'):
	ranlib_action = Action("$RANLIBCOM", "$RANLIBCOMSTR");
	action_list.append(ranlib_action);
pic_lib = Builder(	action = action_list,
			emitter = '$LIBEMITTER',
			prefix = '$LIBPREFIX',
			suffix = '$LIBSUFFIX',
			src_suffix = '$OBJSUFFIX',
			src_builder = 'SharedObject')
env.Append(BUILDERS = {'StaticSharedLibrary': pic_lib});


#-----------------------------------------------------------------------------

ref_node = 'ref/' + env['BUILD'] + '-' + platform.system() + '-' + platform.machine() + '/';
BuildDir(ref_node, '.', duplicate=0)

env.Append(CPPPATH = os.getcwd() + '/include');
env.Append(LIBPATH = os.getcwd() + '/' + ref_node);

if env['WITH_GLIB'] == 'true':
	SConscript(ref_node + 'SConscript.libpgmex');
SConscript(ref_node + 'SConscript.libpgm');
if env['WITH_HTTP'] == 'true':
	SConscript(ref_node + 'SConscript.libpgmhttp');
if env['WITH_SNMP'] == 'true':
	SConscript(ref_node + 'SConscript.libpgmsnmp');
if env['WITH_TEST'] == 'true':
	SConscript(ref_node + 'test/SConscript');
if env['WITH_EXAMPLES'] == 'true':
	SConscript(ref_node + 'examples/SConscript');

# end of file
